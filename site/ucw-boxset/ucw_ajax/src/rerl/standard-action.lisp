;; -*- lisp -*-

(in-package :it.bese.ucw)

(enable-bracket-reader)

;;;; ** Call, Answer, Defaction and Defentry-Point

;;;; Call/Answer vodoo

(defmacro call (component-type &rest component-init-args)
  "Stop the execution of the current action and pass control to
a freshly created component of type COMPONENT-TYPE.

COMPONENT-INIT-ARGS are passed directly to the underlying
make-instance call. This form will return if and when the call'd
component calls answer, the value returned by this form is
whatever the call'd component passed to answer.

Notes:

This macro assumes that the lexcial variable UCW:SELF is bound to
the calling component."
  (rebinding (component-type)
    `(etypecase ,component-type
       ((or symbol standard-component-class)
        (call-component self (make-instance ,component-type ,@component-init-args)))
       (component
        (call-component self ,component-type)))))

(defmacro jump (component-type &rest component-init-args)
  `(jump-to-component (make-instance ,component-type ,@component-init-args)))

(defmacro answer (value)
  "Return control to the calling component passing it VALUE.

Calls to the answer macro should be in a tail position."
  `(answer-component self ,value))

(defmacro call-as-window (component-type &rest component-init-args)
  "Just like CALL but the new component is used for the entire
window. In other words the new component will be used to render
the entire browser window, independant of the current component's
position. The new component must therefore remember to render as
an entire html page.

This is useful for dealing with modal component like alerts and
dialogs which must be dealt with before the user can continue."
  `(call-component (context.window-component *context*)
                   (make-instance ,component-type ,@component-init-args)))

(defmethod handle-action-in-session ((action function) (application standard-application) session frame)
  (with-action-error-handler ()
    (ucw.rerl.dispatcher.dribble "About to call the action")
    (funcall action)))

;;;; Binding request params to variables

(defmacro with-request-params (request-lambda-list request &body body)
  "Bind, according the REQUEST-LAMBDA-LIST the parameters in
  REQUEST and execute BODY.

REQUEST-LAMBDA-LIST is a list of the form:

 ( [ ( symbol string ) | symbol ]
   [ default-value [ supplied-symbol-name ]? ]? )

If the request contains a param (no distinction between GET and
POST params is made) named STRING (which defaults to the symbol
name of SYMBOL) the variable SYMBOL is bound to the associated
value (which is always a string) . If no parameter with that name
was passed SYMBOL will be bound to DEFAULT-VALUE and the variable
named SUPPLIED-SYMBOL-NAME will be bound to NIL.

NB: Parameter names are matched case insensitively."
  (gen-request-param-binder request-lambda-list request body))

(defstruct arg-spec
  symbol
  name-string
  default-value
  supplied-symbol-name)

(defun gen-request-param-binder (args request body)
  (let ((args (mapcar (lambda (arg-spec)
                        (destructuring-bind ((name-symbol &optional name-string) &optional default-value supplied-p)
                            arg-spec
                          (make-arg-spec :symbol name-symbol
                                         :name-string (if name-string
                                                          name-string
                                                          (string-downcase (string name-symbol)))
                                         :default-value default-value
                                         :supplied-symbol-name supplied-p)))
                      (mapcar (lambda (arg-spec)
                                "normalize the arg specs"
                                (cons (ensure-list (car (ensure-list arg-spec)))
                                      (cdr (ensure-list arg-spec))))
                              args))))
    (let ((req (gensym)))
      `(let ((,req ,request))
         (declare (ignorable ,req))
         (let
             ;; bind the vars to their default values
             ,(iterate
                 (for arg-spec in args)
                 (collect (list (arg-spec-symbol arg-spec)
                                (arg-spec-default-value arg-spec)))
                 (awhen (arg-spec-supplied-symbol-name arg-spec)
                   (collect (list it nil))))
           ,@(mapcar (lambda (arg-spec)
                       `(awhen (get-parameter (context.request *context*) ,(arg-spec-name-string arg-spec))
                          (setf ,(arg-spec-symbol arg-spec) it)
                          ,(awhen (arg-spec-supplied-symbol-name arg-spec)
                             `(setf ,it t))))
                   args)
         ,@body)))))

;;;; Defining actions and entry points

(defmacro %defaction (name &rest args)
  "Defines an action method named NAME."
  (let ((qualifier '()))
    (when (symbolp (first args))
      (setf qualifier (list (pop args))))
    (destructuring-bind (((self self-class) &rest other-args) &body body)
        args
      `(defmethod/cc ,name ,@qualifier ((,self ,self-class) ,@other-args)
                     ;; extract declares and docstirngs from BODY and put them here
                     ,@(iterate (for form in body)
                                (while (or (stringp form) 
                                           (and (listp form) (eql 'cl:declare (car form)))))
                                (collect form)
                                (pop body))
                     (let ((self ,self))
                       (ucw.component.action.info
                        (format nil "Serving action (誉梁湖誉誉ㄣ犰飙蝈聃弩舡疳翳箦戽篦犷牒后栾螋弩舡疳汶徵瀛铋汶钺礤簌礅镬疳汶徵钺礤┅篝蜷铉钺礤箦戽扉篝括狎铄箝哄趄徙舡狎珲礤铘钺礤雉桢颦狎珞横祆秣箴邈獒扉弪舂┅ㄢ祜汶钺礤棱镤┅┅┅ㄤ彐磲泸溴驷泗轱钺礤蝈篝蝈篝ㄩㄥ聃犰ㄣ狎蝈篝洪箫灬翦鏖翳躅轳蹂钺礤礤盹殇礤盹礤盹痱弩孱舡皓啜溴驷泗轱钺礤箦泔钿蝈篝戾è礤盹殇篝蜚狒磲疸狎蜚躜蝙＇骢钽犰泔铘屮舄扉篝＇骈钿箦篌轱瞽殇＇骈钿骝犴瀛殇＇骈钿徙糸镱殇┅┅眭祠轲戾鲠祯瀛忾钿ì礤盹礤盹痱弩孱舡皓ㄧ弭栳箬礤盹殇ㄣ镯痫铄铘轶镬狒瀛栳箬ㄣ後箦泔钿蝈篝┅┅ㄩ礤盹痱弩孱舡礤盹箦翩ㄧ弭栳箬礤盹殇ㄣ镯痫铄铘轶镬狒瀛栳箬ㄣ後箦泔钿蝈篝┅┅痱镧ㄣ滗蝈篝┅┅┅┅啜ヤ彐徙糸镱钺礤莉弩舂┅ㄤ彐磲泸溴驽铘蝙痫轭躜é脲狃痨殂狒轱ㄣ灬篌躜飙溟箴狒汨弪痱轱蜷豉铋痱轱蜷豉皓蝈聃弩舡灬礅溽扉篝怙澌怙澌⒛彐轭犷孱趄痫轭怙躅麸翳躜找镦豉疱锰劣赢找眭篝忮篝蜷铉麒殂璎麒孱狃疱钿邃麸列刑擅猎上围躜飙痱彐轼箴邈殒殄翳泔眇戾翦躜骘翳轶孱趄痫轭舢锰劣轶簌礅镬钺黹铉翳溟箴狒汨弪沆狍螽列刑擅猎上ㄥ鲠祯狒邃箴邈殒殄翳狃痨殂狒轱镡赍泗翳轶孱趄痫轭鏖祆忮狒翎汨邃麸涉紊溴驷蹯舡狃痨殂狒轱瞠鏖祆忮躞邃雉桢蝼轶轸眭篝忮翳钺礤镦犷屮轶糸铉狃痨殂狒轱町遗颜庞原塘吐牧躺釉轶蝈聃弩灬礅溽扉篝狍疱咨匀遗颜庞原辛伊陀澡怙澌镦翳孱趄痫轭轶屮邈豸邃麒孱弼弪翳箦蝣弪蝈汩弼弩蝈聃弩骘翳箴邈殒殄躜飚澡怙澌汜泔铘衢汜祆麸泔眇镱孱趔怩眭篝铒泔铘衢犷篦弪骘蝽螽涉翳忉汶孱篚痧矧趔轸ㄡ蜥铄殇犷狍弪鲥翳孱翳躜轶狨麸磲糸汜祆蝈玳篝弪邃鏖翳翳忉汶孱洮雉桢蝼轶盹溥扉箴溴鲥祜疱蝮眭篝磲铛犰禊泔铈殓躜翳忉汶孱洚鏖翳躅轳蹂钺礤ㄡ痧ㄡ篌弪篝蜷铉躜飑躜飑⑴铘蝙痫轭躜祗眭篝忮篝蜷铉蟋轶铒犰祜麇洚躜飑啜戾è狃矧狃痨殂狒轱溴驷蹯舡狃痨殂狒轱瞠┅ㄩ狃箦翩换骈蝮溴戾翦犷孱趄痫轭趔镦豉疱锰劣鏖翳换躜飙篝蜷铉找ㄡ痧扉汜糸镱溟箴狒汨弪狃皓ㄤ屐弭瀛殒灬礅溽ㄥ皓ㄡ钿ㄥ耢ㄣ灬篌钺礤ㄣ灬篌镦屦┅К沆狍螬篝蜷铉躜箪雉鲠祯屦躜飙篝蜷铉┅┅ㄡ痧扉汜糸镱溟箴狒汨弪狃皓换铒徜翳孱趄痫轭ㄡ痧扉汜糸镱溟箴狒汨弪狃皓ㄡ痧孱ㄡ痧扉汜糸镱溟箴狒汨弪狃皓扉篝磲脲轭篝犷沐К沆狍括麒孱痱轱蜷豉啜ê痱轱蜷豉痱轱蜷豉┅乎蜢篝蜷铉躜鸿犷潇弪灬礅溽ī鏖翳汜祆沣戾è箦戽铋飑鏖翳蝈聃弩舡疳蜥眢蝈聃弩舡灬礅溽扉篝ㄣ镱翦舢蝈聃弩泔铘屮舄棱镤┅┅┅┅ㄩ狃痨殂狒轱啜弪蝻⑽狃痨殂狒轱钺礤骘躅洚К狃痨殂狒轱瞟啜弪蝻⑽狃痨殂狒轱箴邈殒殄犷呐屏仗原列刑擅猎上为轶紊坍┅躜飑┅ㄤ彐躅徙糸镱栩彐ㄡ泗轱脲ㄣ镯痫铄铘ㄣ镱翦舢鏖钿秣泔眇镱孱泔铘屮舄┅ㄦ蜥礤ㄣ镱翦舢沲蝌孱舡骝犴泔铘屮舄┅⑶轹孱犷徙糸镱蝈趱蝾犷找ㄡ篝蜷铉麒殂鏖祆汜祆翳徙糸镱灬礅溽澡孟托衔盼疳蜥礤翦轶疳篌邃溟蝈泗禊麸孟托赵怒找态埔镣轶疳篌邃麸土伺闻篆撩陨衔撩陨衔磲忮篝蜷铉灬礅溽矧犷徙糸镱孱趄磲溴鏖翳磲脲徙糸镱ㄤ邈灬蝈豉疱矧徙糸镱孱趄骢钽糸镱徙糸镱┅戾è躜ㄩㄡ钿ㄡ泗轱瞽孱趄徙糸镱ㄡ泗轱瞽彡狲徙糸镱┅戾è躜ㄣ镯瘐翦躜ㄣ镱翦舢狃痨殂狒轱泔铘屮舄横泗轱瞽殇ㄡ泗轱瞽殇徙糸镱┅┅箦翩躜楫疳翳躜椹篝蜚狒躜楫疳翳躜椹彡狲徙糸镱溟箴狒汨弪躜飓┅躜椹ㄣ镯瘐翦躜泔眇镱孱横泗轱瞽殇麒孱徙糸镱ㄥ豉疱汜箦徙糸镱ㄦ躅泗轱ㄡ泗轱瞽殇蝈玳篝弪徙糸镱ê骝犴骝犴瑚轸璀汜祆沣铋飑ㄦ躅汜祆徙糸镱┅┅ㄡ泗轱瞽孱趄ㄡ泗轱瞽殇徙糸镱┅┅┅┅鲠祯弩痱轭舡躜榄麸篝蜷铉躜椹躜椹┅ㄤ彐磲泸徙糸镱栩彐怙澌è蝈篝狎珞脲ㄣ镯痫铄铘Ж泔铘屮舢鏖钿秣泔眇镱孱泔铘屮舄┅ㄦ蜥礤Ж泔铘屮舢沲蝌孱舡骝犴泔铘屮舄┅犰祜鳝雉桢颦脲螬怙澌怙澌蝈礞脲黠蜾狎珞烘蜥礤恒镯痫铄铘啜徙糸镱栩彐蝈玳篝弪徙糸镱狎珞棱镤恒镯痫铄铘泔眇镱孱烘蜥礤骝犴濠ㄤ彐躅徕矧舡蜥鳝蝈聃弩ī翳蝻п怙螋蜥鳝蝈聃弩铋飑ㄤ彐磲泸栳钿戾蜥鳝蝈聃弩è脲ㄣ镱翦铘豉疱⑨痧扉汜糸镱镢翦舡篝蝈犴泔铘孱舡溟箴矬轸轱屮痖蝈蟓轭豸磲徵瀛轭箦泱ㄣ徙桢矧屮痖蝈蟓轭豸磲徵瀛轭箦泱┅鏖翳铄赭矧氕篝蝈犴铋飑鏖翳徙祉飙篝蝈犴铋飑怙澌怙澌⒃栝磲泸栳钿戾蜥蝈聃弩舢组祆箦翳梏麴汜汨泔铘蝻桢徜弪徙泔蜾轭麸翳篚痧扉邃狎珲礤铘蟋箦钿翳桢徜弪蟋屮邈豸翳孪馁犷沆矬翳蝈聃弩舢咨匀闻宰弦谁釉遗镣鏖祆忮怙躅麸翳铄赭矧篝蝈犴躅戾篌轸铋飚阻孱咨匀倭锰吞釉遗镣轶铒铋翳怙澌鏖祆蝓轭箝溴鏖翳徙祉飙篝蝈犴忪镢犷徭翦轸骈铋箬邃轸鏖祆忮痱镳弪禊孱泔溴犷黩轸翦轭麸翳铄赭矧篝蝈犴龄溟糸镱犰禊殒轸轶簌礅镬翳孱轸鏖祆忮怙躅鏖翳翳狒钺礤鏖翳躅轳蹂钺礤蝈聃弩蝈箴镱箦啜戾舄è蝈聃弩ㄣ镱翦舢蝈聃弩泔铘屮舄┅ì蝈箴镱箦ㄣ镱翦舢蝈箴镱箦泔铘屮舄┅括麒孱鏖翳铄赭矧氕篝蝈犴啜ì鏖翳铄赭矧氕篝蝈犴铄赭矧氕篝蝈犴蝈聃弩舂┅括麒孱鏖翳徙祉飙篝蝈犴麒孱ㄥ鏖翳徙祉飙篝蝈犴舂箦翩鏖翳徙祉飙篝蝈犴ㄧ孱簌①撩掏汰釉遗镣┅啜ì鏖翳徙祉飙篝蝈犴磲脲篝蜷铉秕麴豸篝蝈犴┅┅箦翩ㄧ弭桢徜弪蝈箴镱箦⒂翎趱螈⒉鞍纤ㄧ弭桢徜弪蝈箴镱箦⒚镱翦铘赠疱泔铘孱舡豉疱麒孱泔铘孱舡溟箴矬轸轱啜箦翩ㄧ弭桢徜弪蝈箴镱箦⒚镱翦铘拈箴矬轸轱睥泔铘孱舡溟箴矬轸轱瞟ㄩ汜汨啜痱镧括麒孱磲徵瀛轭箦泱啜箦翩ㄧ弭桢徜弪蝈箴镱箦⒚徙桢蔑铘蝻膦篝蜚狒㈨狲徵褰痱轭悱麸篝蜷铉磲徵瀛轭箦泱┅┅括麒孱屮痖蝈蟓轭豸啜箦翩ㄧ弭桢徜弪蝈箴镱箦⑴痖蝈螈ㄤ狒搴躅轹弪筢飙糸礤麸梏麴溽翦屮痖蝈蟓轭豸┅┅啜箦翩ㄧ弭桢徜弪蝈箴镱箦⒚徙桢蔑铘蝻膦㈩锃汜汨瀣铒篝矧澧ㄧ弭桢徜弪蝈箴镱箦⑴痖蝈螈．ㄤ狒搴躅轹弪筢飙糸礤麸梏麴溽翦嘲鞍鞍鞍鞍┅箫礤麒弪轭惫沟ㄣ狒汨п怙螋蜥鳝蝈聃弩ㄨ犷潇弪忾钿è篝蝈犴弪蝻灬礅溽ㄣ麒孱ㄥ篝蝈犴弪蝻颦篝蝈犴悌铄赭矧氕篝蝈犴蝈聃弩舂ㄡ怙螋蜥鳝蝈聃弩舂┅┅ㄩ鏖翳徙祉飙篝蝈犴啜痱镧徙祉旌瑚轸璀徙祉飙篝蝈犴鏖翳徙祉飙篝蝈犴棱镤箦钿桢徜弪蝈箴镱箦黩轸瀛箦聃孱沐篝蜷铉麸镢翦趔ㄧ弭秕麴豸篝蝈犴篝蜷铉鏖翳徙祉飙篝蝈犴ㄥ钽镤轭蝈箴镱箦┅铄赭矧氕篝蝈犴蝈聃弩舂┅啜痱镧箦钿桢徜弪蝈箴镱箦棱镤┅┅ㄣ祜箦蝈聃弩蝈聃弩舂┅ㄤ彐躅徕矧舡彡狲蝈聃弩é镳糸镱犰礤篌徵濠翳蝻п怙螋彡狲蝈聃弩礤篌徵濠换韵南狒糸灬翳孱泔溟铉轭翳溴驷蹯盱桢徜弪泔蹯忮翎脲骝镯ㄥ钽镤轭ㄣ镱翦舢蝈箴镱箦泔铘屮舄┅换轶翳弪骢钽糸镱翳狒泔铞弪趔麸翳狃痱镳獒翦骘蝽狒ㄤ彐磲泸栳钿戾彡狲蝈聃弩è脲盱桢徜弪⒓盔盱鲥蝮轱罱堍碑败孱泔溟铉杰⒄云杠烤ㄣ镱翦铘豉疱Ⅳ屮舣盱鏖翳徙祉飙篝蝈犴舂怙澌怙澌⒃栝磲泸栳钿戾犷潦霖蝈聃弩艉溟筢忪汜汨轭绗箦钿翳桢徜弪蟋黩轸赝汰扰聊乓殒孟卧盼原再信轶堍翦舣盱堍蝓翳孪馁轭鏖翳徙祉飙篝蝈犴犷沆矬翳蝈聃弩舢葬脲汜蝈镦翳盱孱泔溟铉轭翳溴驷蹯桢徜弪汨犷珏狍铄邃邃躅戾篌鏖翳徙祉飙篝蝈犴ㄥ蝌矧⒆稍拳倭锰吞釉遗镣眭篝忮彘翳弪矧簌礅镬麸忮躞邃麸钺礤翳篝蝈犴忾钿轭纰┅啜栳钿戾蜥鳝蝈聃弩ê泔铘孱舡豉疱泔铘孱舡豉疱瑚轸璀徙祉飙篝蝈犴鏖翳徙祉飙篝蝈犴恒徙桢铋飑括麒孱ㄡ钿篝蜷铉泔铘孱舡豉疱篝蜷铉泔铘孱舡豉疱Ⅳ屮舣盱┅啜己狍轶盱桢徜弪┅鏖翳盱簌铘狲坚铙麇戾è篚沣弩箴铋飑礤篌徵铋飑徙祉飙怙澌┅躅鏖钿痱雉邈箦翩礤篌徵ㄣ狒汨п怙螋彡狲蝈聃弩箦翩徙祉飙怙澌鏖翳徙祉飙秕麴豸麸篝蜷铉棱镤麒孱忾钿箦篌轱ㄡ钿铒ㄥ泔铘屮舄乎钼秕钿┅ㄣ镱翦舢箦篌轱泔铘屮舄┅麒孱ㄨ狍弼孱趔骘颦翳瀛沆殄铘箦篌轱瞟箦钿弼孱趔麸翳瀛沆殄铘箦篌轱瞟┅┅箦翩篚沣弩箴舂┅ㄩ篚沣弩箴痱镧蹉鳟蝈蜢彡狲溴怩⒂蹉沐篌骢祆蝈钿弪邃彡狲犷篦弪怙澌戾铉翳轶英戾铉翳徙祉飙怙澌┅己狍轶徙祉飙怙澌简弩蹯Ⅲ蹉沐篌⒕痱镧蹉鳟蝈蜢彡狲溴怩⑵衢戾麸蝈钿弪邃彡狲犷篦弪弪蝻礤篌徵轶英礤篌徵濠简弩蹯㈡衢祯蝈⒕麒孱礤篌徵煎蝌矧礤篌徵己狍梏盱礤篌徵濠京┅┅君┅ㄤ彐躅汜祆汜祆忉汶蟓徙糸镱栩彐ī换翳轶轶篚怙痿轫犰桢蝈轸泔蹯忮泔铙翎铘怩舢ㄡ泗轱瞽栩彐蝈玳篝弪彡狲徙糸镱ê轭鲲汜糸镱轶镬狒邃铋飑换铒鸷麇牾篝栳鲥翳汜祆忉汶汜祆邃恒镯痫铄铘ㄣ镱翦舢鏖钿秣泔眇镱孱泔铘屮舄┅换蔑瘗蜷玷ㄣ舶俺舶暗配麽蜾歪蜚箩蜷铉弪换领蜷玷趔蝈箦蝣邃换换义溟篝蜷怩糸镱犷躞轭箫躜沐犷忾钺蝙骘蝽蟋鏖翳矧鏖翳秕换盹溟骈汜糸镱狎疱蝽轸翦痱秭殇邃翳狒翳骘祆秣轭泔钿轸轱铙狎换礤艉换换义溟篝蜷怩糸镱镦箫躜沐泔溴眭篝蝈翎轭翳徕秭泔瘗蜷玷换铒糸沐翳轶扉篝镦泔钿轸轱铙犷翳骘祆秣轭溟筱灬轫弪换换义溟篝蜷怩糸镱轭忾钺蝙骘蝽眭篝蝈痱镤蹉翳徕秭泔瘗蜷玷换铒糸沐翳轶扉篝镦泔钿轸轱铙犷翳骘祆秣轭溟筱灬轫弪轭翳换滹沲礤铘狒轱犷浏矧雉桢磲翦蜷犰痱秭殇邃鏖翳翳溟篝蜷怩糸镱换换五轸桢翳钺礤镦配麽蜾歪蜚箩蜷铉弪铒屡优铒翳钺礤换镦轸泔铘蜷怩麸蝮磲忮躞邃麸孱滹蝮矧痱镯雉痱镤蹉趔换溴蜷鲥骝镯翳轶箫骠麽蝈鏖翳秕箴邈殒殂痱轱黩轸翦疱蝽轶箝镱换换匀捎酉圃琢遗捎幸现赡拍沦匀孟匈疑侨认棠乓廖孟卧疑抡韵矣换⒘捎廖廖咆幸庞弦赏刑膳琢乙廖陨庞晌锰漳晌乾抡蜗换躺蜕耘韵匀赏刑膳琢乙廖陨庞掀团颐攘卧谅商稍廖粕晕庞葡换辛以擅仗烈姓倚嫌烈纳用塘赏拍晌蜗胖盼尤撂匀孟匈疑侨换献闻弦孟卧疑抡韵矣屡躺谅膛葡廖纳遗迷晌纳遗迷晌蒙呐卧撂换有琶闪态咆磐刑烈佻弦孟斡叛张卧闪牧土桥ㄉ蚊陶纳吻抡蜗换躺蜕耘韵幸厦找磐盼掀诱掠陨哉耘窍夏弦优抑擅庞滔佑掀沼努换牧粤弦幸掀稍踊弦抡由闻佑晌耘乙招陨衔认着峙昧沼拍廖衔廖换匀畔屹掀躺谅商稍佻兹旁扰晌孟卧伊迷釉疑迷躺谅商稍佻弦韵以换ㄉ蚊陶纳吻闻翘汕盼门弦显扰易捎农烈捎晌晌廖琢险掀匀沼换掀匀捎酉圃琢遗胖盼善聊稚优掀匀邢佑陕商稍掀诱萌牧土桥
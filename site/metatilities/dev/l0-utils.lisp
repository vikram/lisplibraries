(in-package #:metatilities)

;;; ----------------------------------------------------------------------------
;;;
;;;   MACROS
;;;

(eval-when (:compile-toplevel :load-toplevel :execute)
  
  ;;; NOTE: can't use WITH-UNIQUE-NAMES here
  ;;; XXX This is a lousy name.  Don't export.
  (defmacro with-standard-printing (&body forms &aux (package (gensym "PACKAGE")))
    "Similar to WITH-STANDARD-IO-SYNTAX, but doesn't change packages."
    `(let ((,package *package*))
       (with-standard-io-syntax
         (let ((*package* ,package))
           ,@forms))))
  
  ) ; eval-always


;;; ----------------------------------------------------------------------------
;;;
;;;   PREDICATES
;;;

#-(or DIGITOOL OPENMCL)
(defun neq (left right)
  (not (eq left right)))

#-(or DIGITOOL OPENMCL)
(declaim (inline neq))

#-(or DIGITOOL OPENMCL)
(define-compiler-macro neq (left right)
  `(not (eq ,left ,right)))

;;; ----------------------------------------------------------------------------
;;;
;;;   FORMING SYMBOLS
;;;

(eval-when (:compile-toplevel :load-toplevel :execute)
  
  (defun form-symbol-in-package (package &rest names)
    "Finds or interns a symbol in package whose name is formed by concatenating the pretty printed representation of the names together."
    (with-standard-printing
      (intern (format nil "狺钺礤螬疳汶徵濠┅ㄤ彐躅骘蝽簌礅镬é蝈篝钺礤螬⑵轭潴矧轭翦蝾簌礅镬轭翳沲蝌孱疳汶徵麒矬钺礤轶骘蝽邃怡泔钽狒孱狒轭翳痱弭豉痱轭翦蝈痱弩孱翎糸镱镦翳钺礤麸珏翳弪鏖翳篝犷溽蜾痱轭糸铉ㄡ痧禊＇骘蝽簌礅镬轭疳汶徵疳汶徵濯钺礤螬┅ㄤ彐躅骘蝽脲黠蜾é蝈篝钺礤螬⑵轭潴矧轭翦蝾簌礅镬轭翳脲黠蜾疳汶徵麒矬钺礤轶骘蝽邃怡泔钽狒孱狒轭翳痱弭豉痱轭翦蝈痱弩孱翎糸镱镦翳钺礤麸珏翳弪鏖翳篝犷溽蜾痱轭糸铉ㄡ痧禊＇骘蝽簌礅镬轭疳汶徵祜徜糸礤鲠祯ㄦ轭洵疳汶徵弘妁黠蜾┅钺礤螬┅ㄤ彐躅骘蝽躅轭翦蝾邃簌礅镬é蝈篝钺礤螬⒚蝈狒弩犷蝈趱蝾犷躅轭翦蝾邃簌礅镬麒矬钺礤轶骘蝽邃怡泔钽狒孱狒轭翳痱弭豉痱轭翦蝈痱弩孱翎糸镱镦翳钺礤麸珏翳弪鏖翳篝犷溽蜾痱轭糸铉磲脲簌礅镬ㄦ矧磲铋狺钺礤螬┅弼犰犰麽换ㄤ彐躅沲蝌孱舡祜徜骈戾ī⒁弭躜铙ㄩ痫篌殁戾翳鲠祯镦翳骈戾沲蝌孱綮忮轭祜徜邃矧骝镯麒殂泔溴轶沲蝌孱綮忮轭弼犰踽翦洚＋犰戾珧屮沆邯箫躜沐疳翳钺礤＋拈玳麸镬ㄩ祜徜疳翳钺礤祜徜疳翳钺礤换澡轶磲脲轸黠螂轭骝邃怩骀弪沣旌祜徜轭绛骈戾箫躜沐骈戾－矧祯汩犰戾珧清铄蜥砒痨矧弪兔泰祜徜疳翳钺礤换ㄤ彐磲泸鏖翳躅轳蹂钺礤è蝈篝鲠蝮怙澌怙澌⒙轭潴翳簌礅镬轭至矣麸珏铙眢沔鏖翳珏铙眢ㄡ篌弪ㄥ鲥蝙＇簌礅镬鲠蝮ī⒚犷蝈忾钿犷屮痱弩箝镱啜戾磲疸狎＇灬礅溽啜ㄧ孱簌愍К┅鲠蝮棱镤┅换ㄤ彐躅孱篚蝈扉篝⑸帏轶扉篝翳孱孱篚蝈扉篝蝈趱蝾轸涉帏轶犷狒镯翳孱翳轶蝈趱蝾箝铉戾麸扉篝泔铘衢铋铉帏喈ㄩ扉篝扉篝┅换换铄黧换换语礤糸礤轸铋沐麸栳鲥秕珏铙眢礤犷箫礤翳轭麒孱换秕蝈蝈徜轭翳磲泸镥疳铙轱镦箫礤骘蝽澡痱镡戾换轶翳狒殒秕玳鲥痱彐轼麸桥斡偻轸蝈磲轭翳痱彐轼换躅糸秕汨犷珏轸换ㄥ鲠飙麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐哄邈豸濠换翳弼犰麒孱轶忮汜躞翳铄黧骢钽糸镱轶躞邃轭屮疳钿轭换圜轸璀鲠蜷徕戾螫犷雉桢磲泸矬忮祜鳟ㄤ彐鲠铄黧憝泔躅翦颡⒚秕铘弪躞邃怡闻子偻骘珏铄蜥糸铉痱轭钺礤螽ㄤ彐躅铄黧é镳糸镱犰痱彐轼⒇┅⒚蝈狒铄躅轭翦蝾邃簌礅镬麒矬痱轭钺礤忮玳铙鏖翳囵蝈骈К麒殂磲忮篝蜷铉矧簌礅镬澡轶溟骀弪骝镯噻孱簌恣轭翳狒翳痱彐轼轶铒篝殂膣躅戾篌篝蜷铉痱彐轼箦翩痱彐轼篝蜷铉痱彐轼┅磲脲簌礅镬ㄦ矧磲铋狺船О洧痱彐轼ㄩ钽铄黧憝泔躅翦颡┅┅换ㄤ彐躅屮痫螋屮痫螋邃簌礅镬ㄦ蝻憝疳汶徵麸疳汶徵濠⑼犭翳屮痫螋邃簌礅镬轭骝镯疳汶徵忮犰箫屮痫螋邃骝镯麸疳汶徵瀹躞瀛疳汶徵骝镯疳汶徵麸疳汶徵濠ㄤ锃屮翦蝾犰簌礅镬簌ㄦ轭洵疳汶徵骝镯疳汶徵濠ㄥ痫螋簌麸疳汶徵濠┅换ㄤ彐珏铄蜷戾铉翳狒戾狍舡翳轭戾铉翳ê滹沲礤铘狒轱⒁弭躜铙趄蹂殒翳轭栳铒驽麇翳犷戾铉翳屐屙孱趔轭轸┅换ㄤ彐礤翳镤戾铉翳狒戾狍舡è翳轭箦聃孱沐戾铉翳窘戾铉翳翳轭绌戾铉翳┅换ㄤ彐礤翳镤戾铉翳狒戾狍舡è翳轭泔铙戾铉翳戾è翦眇翳轭绌祜镳蝈疱狒ū戾铉翳麒殪翦眇滹箦翩翦眇蝈篝翦眇┅铒铛祆翦眇┅┅换ㄤ彐珏铄蜷戾铉翳狒盹篝翳轭戾铉翳ê滹沲礤铘狒轱⒁弭躜铙趄蹂殒翳轭栳铒盹蝈翳犷戾铉翳屐屙孱趔轭轸┅换ㄤ彐礤翳镤戾铉翳狒盹篝è翳轭箦聃孱沐戾铉翳冀戾铉翳翳轭绌戾铉翳┅换ㄤ彐礤翳镤戾铉翳狒盹篝è翳轭泔铙戾铉翳换靠沔戾铉翳狒戾狍舡瓞翳轶箦屙箝黹灬戾è翦眇翳轭绌祜镳蝈疱狒戾铉翳麒殪翦眇滹箦翩翦眇蝈篝翦眇┅铛祆翦眇┅换ㄤ邈灬轫ㄩ铎轭戾铉翳杯扉篝皓ㄤ彐躅戾铉翳杯扉篝⑸扉篝镦戾铉翳笨物翦翳狒翳轶轶忮趑弪翳犷翳钺轹塄塄戾铉翳堠避忮汜躞麇滹瞌铄邃麸趄狯弪箦翳孱糸蝈扉篝ㄡ钿ㄣ镱箴铛祆ㄣ潋┅┅换ㄤ彐躅铄狎禊弪锃镳糸镱犰翳蝈箬镬爱鞍氨┅⒁弭躜铙趄蹂殒帏轶鏖翳轭翳蝈箬镬镦颁爱ㄤ邈灬蝈镳糸黹箴邋畅箴徙畅ㄤ邂蹒癌筢驽豉癌ㄤ钺黹悱屮翦铘翳蝈箬镬洎换谅泔铙弩ㄩ爱翳蝈箬镬翳蝈箬镬洎┅＋藻篝糸礤轸ê蝈痫螋舂祜镳蝈疱狒卑鞍鞍滹铄狎禊弪锃卑暴铄狎禊弪锃爱鞍鞍暴铄狎禊弪锃爱鞍鞍暴┅换ㄤ彐躅铄狎禊羼踽飙翳蝈箬镬洎⒁弭躜铙趄蹂殒犷狎鏖翳轭翳蝈箬镬镦遽汨雉桢虍ㄤ邈灬蝈镳糸黹箴邋畅箴徙畅ㄤ邂蹒癌筢驽豉癌ㄤ钺黹悱屮翦铘翳蝈箬镬洎豉疱滹踱戾骒镝翳蝈箬镬洎戾è翦眇爱颁癌ㄤ邈灬蝈豉疱滹踱戾骒镝翦眇ㄤ钺黹悱屮翦铘翦眇┅ㄣ镱è箦翩翦眇翳滹踱戾骒镝ō┅翦眇翳蝈箬镬洎箦翩翦眇翳滹踱戾骒镝ō┅翦眇翳蝈箬镬洎┅┅＋藻篝糸礤轸ê蝈痫螋舂祜镳蝈疱狒卑鞍鞍滹铄狎禊羼踽飙卑卑爱鞍氨铄狎禊羼踽飙卑渤床炒卑渤床炒爱鞍氨┅换换麒轸弩疳沐换ㄤ彐疳蜥礤翦麒轸弩疳沐汨狎徙翦蝮扉篝＼羽徙＼五黛轭＼葬＼嗅珏＼熙祆＼涕铄驽邃┅换ㄤ彐躅麒轸弩疳沐ㄣ栳颟⒁弭躜铙趄蹂殒汨狎轶犷屐屙孱镦麒轸弩疳沐汨狎徙翦蝮犷铋雉桢蝼轶瀹铒铛祆ㄦ轭汨狎麒轸弩疳沐汨狎徙翦蝮呼弩＇汨狎僵┅换换滹趑邃疳轵换ㄤ彐躅滹趑邃疳轵瘐翎糸鲥疳轵⒁弭躜铙趄蹂殒犷镱禊殒囵豸狒轹瀛疳轵轶滹趑邃扉篝僧瀹殒囵豸狒轹瀛疳轵轶泔铙沐祆鏖翳铒瞽铋沅虍ㄡ钿ㄣ镱箴瘐翎糸鲥疳轵ㄣ潋瘐翎糸鲥疳轵铒ㄣ镱箴ㄣ潋瘐翎糸鲥疳轵┅┅＋物换靠盹鲥麸翦篝篚轸ㄤ彐翦篝篚轸翦篝滹趑邃疳轵īīê翦篝è孱篚蝈ㄤ雉翦洵疳轵Ж猢┅è孱篚蝈铒ㄤ雉翦洵疳轵Ж猢┅┅è孱篚蝈铒ㄤ雉翦洵疳轵横┅┅è孱篚蝈铒ㄤ雉翦洵疳轵Ж悌┅┅è孱篚蝈铒ㄤ雉翦洵疳轵铋飑┅┅